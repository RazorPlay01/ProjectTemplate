plugins {
    id 'java'
    id 'com.gradleup.shadow' version '8.3.0'
    id("io.papermc.paperweight.userdev") version "1.7.1"
    id("xyz.jpenilla.run-paper") version "2.3.1"
}

group = 'com.github.razorplay01'
version = project.project_version

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven { url "https://repo.dmulloy2.net/repository/public/" }
    maven { url = "https://repo.aikar.co/content/groups/aikar/" }
}

dependencies {
    compileOnly("io.papermc.paper:paper-api:1.21-R0.1-SNAPSHOT")
    paperweight.paperDevBundle('1.21.1-R0.1-SNAPSHOT')
    implementation('co.aikar:acf-paper:0.5.1-SNAPSHOT')
    implementation('co.aikar:taskchain-bukkit:3.7.2')
    implementation('com.google.code.findbugs:jsr305:3.0.2')
    implementation project(':TemplateCommon')
}

tasks {
    runServer {
        minecraftVersion("1.21.1")
    }
}

shadowJar {
    relocate 'co.aikar.taskchain', 'com.github.razorplay01.template.taskchain'
    relocate "co.aikar.commands", "com.github.razorplay01.template.acf"
    relocate "co.aikar.locales", "com.github.razorplay01.template.locales"
    relocate "com.github.razorplay.packet_handler", "com.github.razorplay01.template.packet_handler"
}

tasks.build {
    dependsOn shadowJar
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: project.version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
